const std = @import("std");
const session_model = @import("session_model.zig");

pub const TabFocusKind = enum {
    split,
    float,
};

pub const TabMeta = struct {
    uuid: [32]u8,
    name_owned: []u8,

    pub fn deinit(self: *TabMeta, allocator: std.mem.Allocator) void {
        allocator.free(self.name_owned);
    }
};

pub const PaneShellInfo = struct {
    cmd: ?[]u8 = null,
    cwd: ?[]u8 = null,
    status: ?i32 = null,
    duration_ms: ?u64 = null,
    jobs: ?u16 = null,
    running: bool = false,
    started_at_ms: ?u64 = null,

    pub fn deinit(self: *PaneShellInfo, allocator: std.mem.Allocator) void {
        if (self.cmd) |c| allocator.free(c);
        if (self.cwd) |c| allocator.free(c);
        self.* = .{};
    }
};

pub const PaneProcInfo = struct {
    name: ?[]u8 = null,
    pid: ?i32 = null,

    pub fn deinit(self: *PaneProcInfo, allocator: std.mem.Allocator) void {
        if (self.name) |n| allocator.free(n);
        self.* = .{};
    }
};

pub const FrontendSessionCache = struct {
    allocator: std.mem.Allocator,
    session_uuid: [32]u8,
    session_name_owned: []u8,
    tab_counter: usize = 0,
    active_tab: usize = 0,
    active_float_uuid: ?[32]u8 = null,
    focused_pane_uuid: ?[32]u8 = null,
    attached_snapshot: ?session_model.SessionSnapshot = null,
    tabs: std.ArrayList(TabMeta),
    pane_shell: std.AutoHashMap([32]u8, PaneShellInfo),
    pane_proc: std.AutoHashMap([32]u8, PaneProcInfo),
    pane_names: std.AutoHashMap([32]u8, []u8),
    tab_last_floating_uuid: std.ArrayList(?[32]u8),
    tab_last_focus_kind: std.ArrayList(TabFocusKind),

    pub fn init(
        allocator: std.mem.Allocator,
        session_uuid: [32]u8,
        session_name: []const u8,
    ) !FrontendSessionCache {
        return .{
            .allocator = allocator,
            .session_uuid = session_uuid,
            .session_name_owned = try allocator.dupe(u8, session_name),
            .tabs = .empty,
            .pane_shell = std.AutoHashMap([32]u8, PaneShellInfo).init(allocator),
            .pane_proc = std.AutoHashMap([32]u8, PaneProcInfo).init(allocator),
            .pane_names = std.AutoHashMap([32]u8, []u8).init(allocator),
            .tab_last_floating_uuid = .empty,
            .tab_last_focus_kind = .empty,
        };
    }

    pub fn deinit(self: *FrontendSessionCache) void {
        if (self.attached_snapshot) |*snapshot| snapshot.deinit();
        for (self.tabs.items) |*tab| tab.deinit(self.allocator);
        self.tabs.deinit(self.allocator);
        {
            var it = self.pane_shell.iterator();
            while (it.next()) |entry| {
                entry.value_ptr.deinit(self.allocator);
            }
            self.pane_shell.deinit();
        }
        {
            var it = self.pane_proc.iterator();
            while (it.next()) |entry| {
                entry.value_ptr.deinit(self.allocator);
            }
            self.pane_proc.deinit();
        }
        {
            var it = self.pane_names.iterator();
            while (it.next()) |entry| {
                self.allocator.free(entry.value_ptr.*);
            }
            self.pane_names.deinit();
        }
        self.tab_last_floating_uuid.deinit(self.allocator);
        self.tab_last_focus_kind.deinit(self.allocator);
        self.allocator.free(self.session_name_owned);
        self.* = undefined;
    }

    pub fn sessionName(self: *const FrontendSessionCache) []const u8 {
        return self.session_name_owned;
    }

    pub fn sessionUuid(self: *const FrontendSessionCache) [32]u8 {
        return self.session_uuid;
    }

    pub fn setSessionIdentity(
        self: *FrontendSessionCache,
        session_uuid: [32]u8,
        session_name: []const u8,
    ) !void {
        const name_owned = try self.allocator.dupe(u8, session_name);
        self.allocator.free(self.session_name_owned);
        self.session_name_owned = name_owned;
        self.session_uuid = session_uuid;

        if (self.attached_snapshot) |*snapshot| {
            self.allocator.free(snapshot.session_name);
            snapshot.session_name = try self.allocator.dupe(u8, session_name);
            snapshot.uuid = session_uuid;
        }
    }

    pub fn setTabCounter(self: *FrontendSessionCache, tab_counter: usize) void {
        self.tab_counter = tab_counter;
        if (self.attached_snapshot) |*snapshot| {
            snapshot.tab_counter = tab_counter;
        }
    }

    pub fn takeNextTabCounter(self: *FrontendSessionCache) usize {
        const current = self.tab_counter;
        self.tab_counter = if (current < 999) current + 1 else 0;
        if (self.attached_snapshot) |*snapshot| {
            snapshot.tab_counter = self.tab_counter;
        }
        return current;
    }

    pub fn activeTab(self: *const FrontendSessionCache, tab_count: usize) usize {
        if (tab_count == 0) return 0;
        return @min(self.active_tab, tab_count - 1);
    }

    pub fn setActiveTab(self: *FrontendSessionCache, active_tab: usize) void {
        self.active_tab = active_tab;
        if (self.attached_snapshot) |*snapshot| {
            snapshot.active_tab = active_tab;
        }
    }

    pub fn activeFloatUuid(self: *const FrontendSessionCache) ?[32]u8 {
        return self.active_float_uuid;
    }

    pub fn setActiveFloatUuid(self: *FrontendSessionCache, uuid: ?[32]u8) void {
        self.active_float_uuid = uuid;
        if (self.attached_snapshot) |*snapshot| {
            snapshot.active_float_uuid = uuid;
        }
    }

    pub fn focusedPaneUuid(self: *const FrontendSessionCache) ?[32]u8 {
        return self.focused_pane_uuid;
    }

    pub fn setFocusedPaneUuid(self: *FrontendSessionCache, uuid: ?[32]u8) void {
        self.focused_pane_uuid = uuid;
        if (self.attached_snapshot) |*snapshot| {
            snapshot.focused_pane_uuid = uuid;
        }
    }

    pub fn replaceAttachedSnapshotOwned(
        self: *FrontendSessionCache,
        snapshot: session_model.SessionSnapshot,
    ) !void {
        if (self.attached_snapshot) |*old| old.deinit();
        self.attached_snapshot = snapshot;
        try self.setSessionIdentity(snapshot.uuid, snapshot.session_name);
        self.setTabCounter(if (snapshot.tab_counter > 1000) 0 else snapshot.tab_counter);
        self.setActiveTab(snapshot.active_tab);
        self.setActiveFloatUuid(snapshot.active_float_uuid);
        self.setFocusedPaneUuid(snapshot.focused_pane_uuid);
        try self.replaceTabMetaFromSnapshot(snapshot.tabs.items);
        try self.resetTabFocusMemory(snapshot.tabs.items.len);
    }

    pub fn clearAttachedSnapshot(self: *FrontendSessionCache) void {
        if (self.attached_snapshot) |*snapshot| snapshot.deinit();
        self.attached_snapshot = null;
    }

    pub fn clearTabMeta(self: *FrontendSessionCache) void {
        for (self.tabs.items) |*tab| tab.deinit(self.allocator);
        self.tabs.clearRetainingCapacity();
    }

    pub fn replaceTabMetaFromSnapshot(
        self: *FrontendSessionCache,
        tabs: []const session_model.SessionTab,
    ) !void {
        self.clearTabMeta();
        for (tabs) |tab| {
            try self.appendTab(tab.uuid, tab.name);
        }
    }

    pub fn appendTab(
        self: *FrontendSessionCache,
        uuid: [32]u8,
        name: []const u8,
    ) !void {
        try self.tabs.append(self.allocator, .{
            .uuid = uuid,
            .name_owned = try self.allocator.dupe(u8, name),
        });
    }

    pub fn removeTab(self: *FrontendSessionCache, index: usize) void {
        if (index >= self.tabs.items.len) return;
        var removed = self.tabs.orderedRemove(index);
        removed.deinit(self.allocator);
    }

    pub fn tabUuid(self: *const FrontendSessionCache, index: usize) ?[32]u8 {
        if (index >= self.tabs.items.len) return null;
        return self.tabs.items[index].uuid;
    }

    pub fn tabName(self: *const FrontendSessionCache, index: usize) ?[]const u8 {
        if (index >= self.tabs.items.len) return null;
        return self.tabs.items[index].name_owned;
    }

    pub fn setPaneShell(
        self: *FrontendSessionCache,
        uuid: [32]u8,
        cmd: ?[]const u8,
        cwd: ?[]const u8,
        status: ?i32,
        duration_ms: ?u64,
        jobs: ?u16,
    ) void {
        var entry = self.pane_shell.getPtr(uuid);
        if (entry == null) {
            self.pane_shell.put(uuid, .{}) catch return;
            entry = self.pane_shell.getPtr(uuid);
        }
        if (entry) |info| {
            if (cmd) |c| {
                if (info.cmd) |old| self.allocator.free(old);
                info.cmd = self.allocator.dupe(u8, c) catch info.cmd;
            }
            if (cwd) |c| {
                if (info.cwd) |old| self.allocator.free(old);
                info.cwd = self.allocator.dupe(u8, c) catch info.cwd;
            }
            if (status) |s| info.status = s;
            if (duration_ms) |d| info.duration_ms = d;
            if (jobs) |j| info.jobs = j;
        }
    }

    pub fn setPaneShellRunning(
        self: *FrontendSessionCache,
        uuid: [32]u8,
        running: bool,
        started_at_ms: ?u64,
        cmd: ?[]const u8,
        cwd: ?[]const u8,
        jobs: ?u16,
    ) void {
        var entry = self.pane_shell.getPtr(uuid);
        if (entry == null) {
            self.pane_shell.put(uuid, .{}) catch return;
            entry = self.pane_shell.getPtr(uuid);
        }
        if (entry) |info| {
            info.running = running;
            if (started_at_ms) |t| info.started_at_ms = t;
            if (cmd) |c| {
                if (info.cmd) |old| self.allocator.free(old);
                info.cmd = self.allocator.dupe(u8, c) catch info.cmd;
            }
            if (cwd) |c| {
                if (info.cwd) |old| self.allocator.free(old);
                info.cwd = self.allocator.dupe(u8, c) catch info.cwd;
            }
            if (jobs) |j| info.jobs = j;
        }
    }

    pub fn clearPaneShellStartedAt(self: *FrontendSessionCache, uuid: [32]u8) void {
        if (self.pane_shell.getPtr(uuid)) |info| {
            info.started_at_ms = null;
        }
    }

    pub fn getPaneShell(self: *const FrontendSessionCache, uuid: [32]u8) ?PaneShellInfo {
        return self.pane_shell.get(uuid);
    }

    pub fn setPaneProc(self: *FrontendSessionCache, uuid: [32]u8, name: ?[]const u8, pid: ?i32) void {
        var entry = self.pane_proc.getPtr(uuid);
        if (entry == null) {
            self.pane_proc.put(uuid, .{}) catch return;
            entry = self.pane_proc.getPtr(uuid);
        }
        if (entry) |info| {
            if (name) |n| {
                if (info.name) |old| self.allocator.free(old);
                info.name = self.allocator.dupe(u8, n) catch info.name;
            }
            if (pid) |p| info.pid = p;
        }
    }

    pub fn getPaneProc(self: *const FrontendSessionCache, uuid: [32]u8) ?PaneProcInfo {
        return self.pane_proc.get(uuid);
    }

    pub fn removePaneProc(self: *FrontendSessionCache, uuid: [32]u8) void {
        if (self.pane_proc.fetchRemove(uuid)) |kv| {
            var info = kv.value;
            info.deinit(self.allocator);
        }
    }

    pub fn setPaneNameOwned(self: *FrontendSessionCache, uuid: [32]u8, name_owned: []u8) void {
        if (self.pane_names.get(uuid)) |old_name| {
            self.allocator.free(old_name);
        }
        self.pane_names.put(uuid, name_owned) catch self.allocator.free(name_owned);
    }

    pub fn paneName(self: *const FrontendSessionCache, uuid: [32]u8) ?[]const u8 {
        return self.pane_names.get(uuid);
    }

    pub fn hasPaneName(self: *const FrontendSessionCache, uuid: [32]u8) bool {
        return self.pane_names.contains(uuid);
    }

    pub fn removePaneName(self: *FrontendSessionCache, uuid: [32]u8) void {
        if (self.pane_names.fetchRemove(uuid)) |kv| {
            self.allocator.free(kv.value);
        }
    }

    pub fn resetTabFocusMemory(self: *FrontendSessionCache, tab_count: usize) !void {
        self.tab_last_floating_uuid.clearRetainingCapacity();
        self.tab_last_focus_kind.clearRetainingCapacity();
        for (0..tab_count) |_| {
            try self.tab_last_floating_uuid.append(self.allocator, null);
            try self.tab_last_focus_kind.append(self.allocator, .split);
        }
    }

    pub fn appendTabFocusMemory(self: *FrontendSessionCache) !void {
        try self.tab_last_floating_uuid.append(self.allocator, null);
        try self.tab_last_focus_kind.append(self.allocator, .split);
    }

    pub fn removeTabFocusMemory(self: *FrontendSessionCache, index: usize) void {
        if (index < self.tab_last_floating_uuid.items.len) {
            _ = self.tab_last_floating_uuid.orderedRemove(index);
        }
        if (index < self.tab_last_focus_kind.items.len) {
            _ = self.tab_last_focus_kind.orderedRemove(index);
        }
    }

    pub fn clearTabFocusMemory(self: *FrontendSessionCache) void {
        self.tab_last_floating_uuid.clearRetainingCapacity();
        self.tab_last_focus_kind.clearRetainingCapacity();
    }

    pub fn rememberFloatingFocus(
        self: *FrontendSessionCache,
        active_tab: usize,
        pane_uuid: [32]u8,
    ) void {
        if (active_tab >= self.tab_last_floating_uuid.items.len) return;
        self.tab_last_floating_uuid.items[active_tab] = pane_uuid;
        if (active_tab < self.tab_last_focus_kind.items.len) {
            self.tab_last_focus_kind.items[active_tab] = .float;
        }
    }

    pub fn rememberSplitFocus(self: *FrontendSessionCache, active_tab: usize) void {
        if (active_tab < self.tab_last_focus_kind.items.len) {
            self.tab_last_focus_kind.items[active_tab] = .split;
        }
    }

    pub fn lastFocusKind(
        self: *const FrontendSessionCache,
        active_tab: usize,
    ) ?TabFocusKind {
        if (active_tab >= self.tab_last_focus_kind.items.len) return null;
        return self.tab_last_focus_kind.items[active_tab];
    }

    pub fn lastFloatingUuid(
        self: *const FrontendSessionCache,
        active_tab: usize,
    ) ?[32]u8 {
        if (active_tab >= self.tab_last_floating_uuid.items.len) return null;
        return self.tab_last_floating_uuid.items[active_tab];
    }
};
