const std = @import("std");
const frontend_attach = @import("frontend_attach.zig");
const FrontendAttachState = @import("frontend_attach_state.zig").FrontendAttachState;
const FrontendClient = @import("frontend_client.zig").SesClient;
const Transport = @import("frontend_client.zig").Transport;
const session_model = @import("session_model.zig");
const SessionProjection = @import("session_projection.zig").SessionProjection;
const wire = @import("wire.zig");

pub const FrontendRuntime = struct {
    pub const ReattachSnapshotResult = struct {
        snapshot: session_model.SessionSnapshot,
        pane_uuids: [][32]u8,
        allocator: std.mem.Allocator,

        pub fn deinit(self: *ReattachSnapshotResult) void {
            self.snapshot.deinit();
            self.allocator.free(self.pane_uuids);
            self.* = undefined;
        }
    };

    allocator: std.mem.Allocator,
    client: FrontendClient,
    projection: SessionProjection,
    attach_state: FrontendAttachState,

    pub fn create(
        allocator: std.mem.Allocator,
        session_id: [32]u8,
        session_name: []const u8,
        keepalive: bool,
        debug: bool,
        log_file: ?[]const u8,
        frontend_kind: wire.FrontendKind,
        transport: Transport,
    ) !*FrontendRuntime {
        const runtime = try allocator.create(FrontendRuntime);
        errdefer allocator.destroy(runtime);

        runtime.allocator = allocator;
        runtime.client = FrontendClient.initWithTransport(
            allocator,
            session_id,
            session_name,
            keepalive,
            debug,
            log_file,
            frontend_kind,
            transport,
        );
        errdefer runtime.client.deinit();

        runtime.projection = try SessionProjection.init(allocator, session_id, session_name);
        runtime.attach_state = .{};
        return runtime;
    }

    pub fn createTerminal(
        allocator: std.mem.Allocator,
        session_id: [32]u8,
        session_name: []const u8,
        debug: bool,
        log_file: ?[]const u8,
        transport: Transport,
    ) !*FrontendRuntime {
        return create(
            allocator,
            session_id,
            session_name,
            true,
            debug,
            log_file,
            .terminal,
            transport,
        );
    }

    pub fn destroy(self: *FrontendRuntime) void {
        const allocator = self.allocator;
        self.projection.deinit();
        self.client.deinit();
        self.* = undefined;
        allocator.destroy(self);
    }

    pub fn reconcileResolvedName(self: *FrontendRuntime) !?frontend_attach.SessionNameChange {
        return frontend_attach.reconcileResolvedName(self.allocator, &self.client, &self.projection);
    }

    pub fn syncSessionIdentity(self: *FrontendRuntime) !?frontend_attach.SessionNameChange {
        return frontend_attach.syncSessionIdentity(self.allocator, &self.client, &self.projection);
    }

    pub fn completeReattach(self: *FrontendRuntime) !?frontend_attach.SessionNameChange {
        return frontend_attach.completeReattach(self.allocator, &self.client, &self.projection);
    }

    pub fn markSessionStolen(self: *FrontendRuntime) void {
        frontend_attach.markSessionStolen(&self.attach_state);
    }

    pub fn beginReattach(self: *FrontendRuntime) void {
        self.attach_state.beginReattach();
    }

    pub fn endReattach(self: *FrontendRuntime) void {
        self.attach_state.endReattach();
    }

    pub fn parseSessionSnapshotJson(self: *FrontendRuntime, session_state_json: []const u8) !session_model.SessionSnapshot {
        return session_model.SessionSnapshot.fromJson(self.allocator, session_state_json);
    }

    pub fn drainPendingSessionSnapshot(self: *FrontendRuntime) ?session_model.SessionSnapshot {
        const session_json = self.client.drainPendingSessionState() orelse return null;
        defer self.allocator.free(session_json);
        return self.parseSessionSnapshotJson(session_json) catch null;
    }

    pub fn reattachSessionSnapshot(self: *FrontendRuntime, session_id_prefix: []const u8) !?ReattachSnapshotResult {
        const result = try self.client.reattachSession(session_id_prefix);
        if (result == null) return null;

        const reattach = result.?;
        errdefer self.allocator.free(reattach.pane_uuids);
        defer self.allocator.free(reattach.session_state_json);

        var snapshot = try self.parseSessionSnapshotJson(reattach.session_state_json);
        errdefer snapshot.deinit();

        return .{
            .snapshot = snapshot,
            .pane_uuids = reattach.pane_uuids,
            .allocator = self.allocator,
        };
    }

    pub fn replaceProjectionFromSnapshot(
        self: *FrontendRuntime,
        snapshot: *const session_model.SessionSnapshot,
        live_tab_count: usize,
    ) !void {
        try self.projection.replaceAttachedSnapshotOwned(try snapshot.clone(self.allocator));
        self.client.session_id = self.projection.sessionUuid();
        self.client.session_name = self.projection.sessionName();
        self.projection.setActiveTab(self.projection.activeTab(live_tab_count));
    }

    pub fn sessionAddTab(
        self: *FrontendRuntime,
        tab_uuid: [32]u8,
        pane_uuid: [32]u8,
        tab_index: usize,
        name: []const u8,
    ) !void {
        try self.client.sessionAddTab(tab_uuid, pane_uuid, tab_index, name);
    }

    pub fn sessionRemoveTab(self: *FrontendRuntime, tab_uuid: [32]u8, active_tab: ?usize) !void {
        try self.client.sessionRemoveTab(tab_uuid, active_tab);
    }

    pub fn sessionSyncFloat(
        self: *FrontendRuntime,
        pane_uuid: [32]u8,
        active_tab: ?usize,
        parent_tab: ?usize,
        visible: bool,
        tab_visible: u64,
        sticky: bool,
        is_pwd: bool,
        float_key: u8,
        width_pct: u8,
        height_pct: u8,
        pos_x_pct: u8,
        pos_y_pct: u8,
        pad_x: u8,
        pad_y: u8,
        active: bool,
    ) !void {
        try self.client.sessionSyncFloat(
            pane_uuid,
            active_tab,
            parent_tab,
            visible,
            tab_visible,
            sticky,
            is_pwd,
            float_key,
            width_pct,
            height_pct,
            pos_x_pct,
            pos_y_pct,
            pad_x,
            pad_y,
            active,
        );
    }

    pub fn sessionRemoveFloat(self: *FrontendRuntime, pane_uuid: [32]u8) !void {
        try self.client.sessionRemoveFloat(pane_uuid);
    }

    pub fn sessionSyncTabLayout(
        self: *FrontendRuntime,
        tab_uuid: [32]u8,
        active_tab: usize,
        focused_pane_uuid: ?[32]u8,
        root_json: []const u8,
    ) !void {
        try self.client.sessionSyncTabLayout(tab_uuid, active_tab, focused_pane_uuid, root_json);
    }
};
